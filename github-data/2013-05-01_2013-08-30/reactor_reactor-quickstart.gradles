/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor Quick-start components'

ext {
	gradleVersion = '1.8'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Languages
	groovyVersion = '2.1.7'

	// Logging
	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.13'

	// Spring
	springVersion = '4.0.0.RC1'
	springBootVersion = '0.5.0.BUILD-SNAPSHOT'
	sdJpaVersion = '1.4.1.RELEASE'

	// Jetty
	jettyVersion = '9.0.1.v20130408'

	// Reactor
	reactorVersion = '1.0.0.BUILD-SNAPSHOT'

	// Hibernate
	hibernateVersion = '4.3.0.Beta4'

	// Testing
	spockVersion = '0.7-groovy-2.0'
}

apply from: "$gradleScriptDir/setup.gradle"

allprojects {
	apply plugin: 'idea'
	apply plugin: 'eclipse'

	configurations.all {
		exclude group: 'commons-logging'
		exclude module: 'junit'
	}

	group = 'org.projectreactor.quickstart'
	repositories {
		//mavenLocal()
		maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
		maven { url 'http://repo.spring.io/libs-snapshot' }
		mavenCentral()
	}
}

subprojects { subproject ->
	apply plugin: 'java'

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:none', '-g']

	// dependencies that are common across all java projects
	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"
		runtime "ch.qos.logback:logback-classic:$logbackVersion"

		// Reactor
		compile "org.projectreactor:reactor-core:$reactorVersion"

		// Testing
		testCompile 'junit:junit-dep:4.11'
		testCompile 'org.hamcrest:hamcrest-library:1.3'
	}

}

project('core') {
	description = 'Reactor quickstart core components'
	dependencies {
	}
}

project('simple') {
	description = 'Reactor quickstart simple event handling example'
	dependencies {
		compile project(':core')
	}
}

project('composable') {
	description = 'Reactor quickstart Composable example'
	dependencies {
		compile project(':core')
	}
}

project('groovy') {
	description = 'Reactor quickstart Groovy example'

	apply plugin: 'groovy'

	dependencies {
		compile project(':core')

		// Groovy
		compile "org.codehaus.groovy:groovy-all:$groovyVersion",
				"org.projectreactor:reactor-groovy:$reactorVersion"
	}
}

project('websocket') {
	description = 'Reactor quickstart websocket example'
	dependencies {
		compile project(':core')

		// WebSocket support
		compile "org.eclipse.jetty:jetty-servlet:$jettyVersion",
				"org.eclipse.jetty.websocket:websocket-server:$jettyVersion"
		compile("javax.net.websocket:javax.net.websocket-api:1.0-b08", provided)
	}
}

project('complex') {
	description = 'Reactor quickstart Complex example'
	dependencies {
		compile project(":core")

		// Reactor
		compile("org.projectreactor.data:data-spring:$reactorVersion") {
			exclude module: 'spring-data-mongodb'
		}

		// Hibernate
		compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"

		// Spring
		compile "org.springframework.boot:spring-boot-starter-websocket:$springBootVersion",
				"org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

		runtime "org.hsqldb:hsqldb:2.3.0"
	}

	task(run, dependsOn: 'classes', type: JavaExec) {
		main = 'reactor.quickstart.complex.ComplexTradeServerExample'
		classpath = sourceSets.main.runtimeClasspath
	}
}



