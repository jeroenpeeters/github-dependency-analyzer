// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.+'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:extra-abs:0.9.+'
    compile 'com.nineoldandroids:library:2.4.0'
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
    }

    productFlavors {
        consumer {
            packageName "com.coinbase.android"
            buildConfigField "com.coinbase.android.BuildType", "type", "com.coinbase.android.BuildType.CONSUMER"
        }

        merchant {
            packageName "com.coinbase.android.merchant"
            buildConfigField "com.coinbase.android.BuildType", "type", "com.coinbase.android.BuildType.MERCHANT"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src-javapusherclient','src-zxing-android','src']
            resources.srcDirs = ['src-javapusherclient','src-zxing-android','src']
            aidl.srcDirs = ['src-javapusherclient','src-zxing-android','src']
            renderscript.srcDirs = ['src-javapusherclient','src-zxing-android','src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        checkReleaseBuilds false
    }
}
