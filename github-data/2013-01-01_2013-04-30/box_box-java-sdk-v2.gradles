apply plugin: "java"
apply plugin: "maven"

group = "com.box"
archivesBaseName = "box-java-sdk"
version = "2.2.2"

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	archives {
		extendsFrom configurations.default
	}
}

dependencies {
	archives "org.apache.maven.wagon:wagon-ssh:2.2"
	compile "com.fasterxml.jackson.core:jackson-annotations:2.2.2"
	compile "com.fasterxml.jackson.core:jackson-core:2.2.2"
	compile "com.fasterxml.jackson.core:jackson-databind:2.2.2"
	compile "commons-codec:commons-codec:1.8"
	compile "commons-io:commons-io:2.4"
	compile "commons-lang:commons-lang:2.6"
	compile "org.apache.httpcomponents:httpclient-cache:4.2.5"
	compile "org.apache.httpcomponents:httpclient:4.2.5"
	compile "org.apache.httpcomponents:httpcore:4.2.4"
	compile "org.apache.httpcomponents:httpmime:4.2.5"
	testCompile "junit:junit:4.11"
	testCompile "org.easymock:easymock:3.1"
}

sourceSets {
	main {
		java.srcDirs = ["src"]
		resources.srcDirs = ["src"]
	}

	test {
		java.srcDirs = ["tst"]
		resources.srcDirs = ["tst"]
	}
}

uploadArchives {
	repositories.mavenDeployer {
		configuration = configurations.archives

		pom.project {
			name "Box Java SDK v2"
			packaging "jar"
			description "The Java SDK for the Box API."
			url "https://github.com/box/box-java-sdk-v2"

			scm {
				url "scm:git@github.com:box/box-java-sdk-v2.git"
				connection "scm:git@github.com:box/box-java-sdk-v2.git"
				developerConnection "scm:git@github.com:box/box-java-sdk-v2.git"
			}

			licenses {
				license {
					name "The Apache Software License, Version 2.0"
					url "http://www.apache.org/licenses/LICENSE-2.0.txt"
					distribution "repo"
				}
			}

			organization {
				name "Box"
				url "https://github.com/box"
			}

			developers {
				developer {
					id "jianbox"
					name "Jian Lin"
					email "jlin@box.com"
				}
			}
		}

		if (project.hasProperty("repoUploadUrl")) {
			repository(url: repoUploadUrl) {
				authentication(userName: repoUserName, privateKey: repoKeyFile)
			}
		}
	}
}
apply plugin: "java"

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile project(":BoxJavaLibraryV2")
}

sourceSets {
	main {
		java.srcDirs = ["src"]
	}
}
